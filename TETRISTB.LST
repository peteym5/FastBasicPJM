2 HLV=16:LV=1:TYPE=1:HT=0:DIM A$(32)
15 GOSUB 3000
20 GOSUB 5000
22 HP=30:IF TYPE=2 OR TYPE=4 THEN HP=0
23 IF TYPE=3 OR TYPE=4 AND LV>1 THEN HT=(LV/2)-1:GOSUB 4000
25 BDLY=0:NP=1+RAND(7):SPD=LV:SPE=SPD:GOSUB 850
26 BRST=4:IF LV>7 THEN BRST=12
27 IF LV>15 THEN BRST=16
30 ? "$)SCORE:","LINES:","LEVEL:":SC=0:SPE=SPD:GOSUB 800:HOZDLY=0
65 REPEAT 
66   COLOR PC:ON PC GOSUB 1000,1040,1080,1120,1160,1200,1240:PAUSE 2
68   POKE 20,250:H=0
70   REPEAT 
75     COLOR 32:ON PC GOSUB 1000,1040,1080,1120,1160,1200,1240:Y=Y+1:SPL=SPL+40
80     COLOR PC
85     REPEAT 
88       IF PEEK(764)=33 THEN GOSUB 900
89       IF PEEK(53279)&1=0 THEN POP :POP :POP :GOTO 15
90       P=STICK(0):GOSUB 500
92       IF STRIG(0)=1
93         BDLY=0
94       ELSE 
96         IF BDLY=0 OR BDLY>=20 THEN GOSUB 550
98         BDLY=BDLY+1:IF BDLY>=21 THEN BDLY=BRST
100       ENDIF 
102       ON PC GOSUB 1000,1040,1080,1120,1160,1200,1240
105     UNTIL PEEK(20)>=TM OR (P&3<>3)
106     IF P&1=0 AND C=0 THEN GOSUB 400
108     POKE 20,0
110   UNTIL C
115   GOSUB 700
120   GOSUB 850
125   ON PC GOSUB 2000,2040,2080,2120,2160,2200,2240
130 UNTIL C OR T
135 FOR N=W TO W+959:POKE N,64+PEEK(53770)&7:NEXT N
140 PAUSE 30
150 GOTO 15
400 COLOR 32:ON PC GOSUB 1000,1040,1080,1120,1160,1200,1240
404 IF Y<16 AND Y<HP-4
405   REPEAT 
410     Y=Y+1:SPL=SPL+40
415   UNTIL Y>15 OR Y>=HP-4
416 ENDIF 
420 REPEAT 
430   SPL=SPL+40:Y=Y+1:ON PC GOSUB 1000,1040,1080,1120,1160,1200,1240
435 UNTIL C
440 RETURN 
500 IF P&12<>12
502   IF HOZDLY=0 OR HOZDLY>=20
504     COLOR 32:ON PC GOSUB 1000,1040,1080,1120,1160,1200,1240
508     IF P&8=0 AND NOT R THEN X=X+1:SPL=SPL+1
512     IF P&4=0 AND NOT L THEN X=X-1:SPL=SPL-1
516   ENDIF 
518   HOZDLY=HOZDLY+1:IF HOZDLY>=21 THEN HOZDLY=BRST
520 ELSE 
524   HOZDLY=0
526 ENDIF 
530 COLOR PC
540 RETURN 
550 DPOKE 53762,44806:PROT=ROT:COLOR 32:ON PC GOSUB 1000,1040,1080,1120,1160,1200,1240:ROT=(ROT+1)&3
560 ON PC GOSUB 2000,2040,2080,2120,2160,2200,2240:IF T THEN ROT=PROT
580 POKE 53762,0:COLOR PC:H=1:RETURN 
600 LV=LV+0.1:LS=LS+ROWSGOT:IF LV MOD 1=0 THEN SPD=LV:GOSUB 5800
610 RETURN 
620 LS=LS-ROWSGOT:IF LS<=0 THEN LS=0:SC=SC+(HT^2)*1000+LV*1000:GOSUB 800:? :? "		SUCCESS":POP :POP :GOTO 135
630 RETURN 
640 LS=LS+ROWSGOT:IF LS<NLS THEN RETURN 
650 LV=LV+1:SC=SC+LV*1000:NLS=NLS+10:GOSUB 800:COLOR 32:SPD=LV
655 FOR N=1 TO 22 STEP 0.5:IF N<19 THEN PLOT 12+RAND(10),INT(N+3)
656   IF N<20 THEN PLOT 12+RAND(10),INT(N+2)
657   IF N<18 THEN PLOT 12+RAND(10),INT(N+RAND(5))
658   IF N<19 THEN PLOT 12+RAND(10),INT(N+1)
659   IF N<18 THEN PLOT 12+RAND(10),INT(N+RAND(5))
660   PLOT 12,N:DRAWTO 21,N
662 NEXT N
665 IF LV>1 THEN HT=(LV/2)-1:GOSUB 4000
668 RETURN 
670 LS=LS+ROWSGOT:IF LS<NLS THEN RETURN 
671 IF LV<10 THEN NLS=NLS+4+LV*2
672 IF LV>9 THEN NLS=NLS+25
673 LV=LV+1:SC=SC+LV*1000:NLS=NLS+4+LV*2:COLOR 32:SPD=SPD+1:GOSUB 655
674 RETURN 
700 SOUND 1,RAND(32)+224,0,12:COLOR PC:ON PC GOSUB 1000,1040,1080,1120,1160,1200,1240:ROWSGOT=0:POKE 77,0
705 Q=W+92:SC=((R>0)+(L>0)+(C>0))*SPE+SC:IF Y<HP THEN HP=Y:Q=W-68+HP*40:IF HP<4 THEN HP=4
708 SOUND 1,0,10,15
710 FOR A=W+892 TO Q STEP -40
712   CNT=0
714   FOR B=A TO A+9
716     IF PEEK(B)>64 THEN CNT=CNT+1
718   NEXT B
720   IF CNT>=10 THEN GOSUB 780:A=A+40
740 NEXT A:IF ROWSGOT THEN ON TYPE GOSUB 600,620,640,670,620,620,600
745 IF ROWSGOT=1 THEN SC=SC+40:FOR N=90 TO 0 STEP -0.5:SOUND 1,6+N&30,10,N/6:NEXT N
750 IF ROWSGOT=2 THEN SC=SC+100:FOR N=150 TO 0 STEP -0.625:SOUND 1,4+N&30,10,N/10:NEXT N
755 IF ROWSGOT=3 THEN SC=SC+300:FOR N=210 TO 0 STEP -0.84:SOUND 1,2+N&31,10,N/14:NEXT N
760 IF ROWSGOT=4 THEN SC=SC+1200:FOR N=255 TO 0 STEP -1:SOUND 1,N&31,10,N/17:NEXT N
765 HP=HP+ROWSGOT:Q=Q+40*ROWSGOT
775 GOSUB 800
779 RETURN 
780 IF Q<W THEN Q=W
785 POKE 53762,32:FOR D=A TO Q STEP -40:MOVE D-40,D,10:NEXT D:POKE 53762,0
790 ROWSGOT=ROWSGOT+1:RETURN 
795 RETURN 
800 ? ".";SC,LS,INT(LV);:RETURN 
850 COLOR 32:X=29:Y=9:SPL=W+X+40*Y:ROT=0:ON NP GOSUB 1000,1040,1080,1120,1160,1200,1240:PC=NP:NP=1+RAND(7)
855 COLOR NP:ON NP GOSUB 1000,1040,1080,1120,1160,1200,1240
860 RESTORE 6000+SPD:READ TM:X=16:Y=0:SPL=W+X+40*Y:IF TM<0 THEN TM=0
865 BDLY=5:IF BDLY>INT(TM/2) THEN BDLY=INT(TM/2)
875 RETURN 
900 REPEAT 
902   POKE 764,255:PAUSE 1
905 UNTIL PEEK(764)=255
906 ? ".		PAUSE";
910 REPEAT 
912   POKE 77,128:POKE 78,246
915 UNTIL PEEK(764)=33
920 POKE 764,255
923 GOSUB 800:POKE 77,0
925 RETURN 
1000 IF ROT=0
1002   PLOT X-1,Y:DRAWTO X+2,Y
1003   L=PEEK(SPL-2):R=PEEK(SPL+3)
1004   C=DPEEK(SPL+39)+DPEEK(SPL+41)
1007 ELSE :IF ROT=1
1010     PLOT X,Y-1:DRAWTO X,Y+2
1011     L=PEEK(SPL-41)+PEEK(SPL-1)+PEEK(SPL+39)+PEEK(SPL+79):C=PEEK(SPL+120)
1013     R=PEEK(SPL-39)+PEEK(SPL+1)+PEEK(SPL+41)+PEEK(SPL+81)
1017   ELSE :IF ROT=2
1020       PLOT X-2,Y:DRAWTO X+1,Y
1021       L=PEEK(SPL-3):R=PEEK(SPL+2)
1023       C=DPEEK(SPL+38)+DPEEK(SPL+40)
1027     ELSE :IF ROT=3
1030         PLOT X,Y-2:DRAWTO X,Y+1
1031         L=PEEK(SPL-81)+PEEK(SPL-41)+PEEK(SPL-1)+PEEK(SPL+39):C=PEEK(SPL+80)
1032         R=PEEK(SPL-79)+PEEK(SPL-39)+PEEK(SPL+1)+PEEK(SPL+41)
1036       ENDIF :ENDIF :ENDIF :ENDIF 
1037 RETURN 
1040 IF ROT=0 OR ROT=2
1041   PLOT X-1,Y:PLOT X,Y:PLOT X,Y+1:PLOT X+1,Y+1
1042   R=PEEK(SPL+1)+PEEK(SPL+42)
1043   C=PEEK(SPL+39)+DPEEK(SPL+80)
1045   L=PEEK(SPL-2)+PEEK(SPL+39)
1047 ELSE 
1050   PLOT X,Y-1:PLOT X-1,Y:PLOT X,Y:PLOT X-1,Y+1
1051   L=PEEK(SPL-41)+PEEK(SPL-2)+PEEK(SPL+38)
1053   R=PEEK(SPL-39)+PEEK(SPL+1)+PEEK(SPL+40)
1055   C=PEEK(SPL+79)+PEEK(SPL+40)
1057 ENDIF 
1058 RETURN 
1080 IF ROT=0 OR ROT=2
1081   PLOT X+1,Y:PLOT X,Y:PLOT X-1,Y+1:PLOT X,Y+1
1082   L=PEEK(SPL-1)+PEEK(SPL+38)
1083   R=PEEK(SPL+2)+PEEK(SPL+41)
1085   C=PEEK(SPL+41)+DPEEK(SPL+79)
1087 ELSE 
1090   PLOT X-1,Y-1:PLOT X-1,Y:PLOT X,Y:PLOT X,Y+1
1091   L=PEEK(SPL-2)+PEEK(SPL-42)+PEEK(SPL+39)
1093   R=PEEK(SPL-40)+PEEK(SPL+1)+PEEK(SPL+41)
1095   C=PEEK(SPL+39)+PEEK(SPL+80)
1097 ENDIF 
1098 RETURN 
1120 IF ROT=0
1121   PLOT X,Y:PLOT X-1,Y+1:DRAWTO X+1,Y+1
1122   L=PEEK(SPL-1)+PEEK(SPL+38)
1123   R=PEEK(SPL+1)+PEEK(SPL+42)
1125   C=PEEK(SPL+79)+DPEEK(SPL+80)
1127 ELSE :IF ROT=1
1130     PLOT X,Y:PLOT X-1,Y-1:DRAWTO X-1,Y+1
1131     L=PEEK(SPL-42)+PEEK(SPL-2)+PEEK(SPL+38)
1133     R=PEEK(SPL-40)+PEEK(SPL+1)+PEEK(SPL+40)
1135     C=PEEK(SPL+40)+PEEK(SPL+79)
1137   ELSE :IF ROT=2
1141       PLOT X,Y:PLOT X-1,Y-1:DRAWTO X+1,Y-1
1142       L=PEEK(SPL-1)+PEEK(SPL-42)
1143       R=PEEK(SPL+1)+PEEK(SPL-38)
1145       C=PEEK(SPL-1)+PEEK(SPL+40)+PEEK(SPL+1)
1148     ELSE 
1150       PLOT X,Y:PLOT X+1,Y-1:DRAWTO X+1,Y+1
1151       L=PEEK(SPL-40)+PEEK(SPL-1)+PEEK(SPL+40)
1153       R=PEEK(SPL-38)+PEEK(SPL+2)+PEEK(SPL+42)
1155       C=PEEK(SPL+40)+PEEK(SPL+81)
1156     ENDIF :ENDIF :ENDIF 
1157 RETURN 
1160 IF ROT=0
1161   PLOT X-1,Y:DRAWTO X+1,Y:PLOT X-1,Y+1
1162   L=PEEK(SPL-2)+PEEK(SPL+38)
1163   R=PEEK(SPL+2)+PEEK(SPL+40)
1165   C=PEEK(SPL+41)+PEEK(SPL+40)+PEEK(SPL+79)
1167 ELSE :IF ROT=1
1170     PLOT X-1,Y-1:PLOT X,Y-1:DRAWTO X,Y+1
1171     L=PEEK(SPL-42)+PEEK(SPL-1)+PEEK(SPL+39)
1173     R=PEEK(SPL-39)-PEEK(SPL+1)+PEEK(SPL+41)
1175     C=PEEK(SPL-1)+PEEK(SPL+80)
1177   ELSE :IF ROT=2
1180       PLOT X+1,Y-1:PLOT X-1,Y:DRAWTO X+1,Y
1181       L=PEEK(SPL-40)+PEEK(SPL-2)
1183       R=PEEK(SPL-38)+PEEK(SPL+2)
1185       C=PEEK(SPL+39)+DPEEK(SPL+40)
1187     ELSE 
1190       PLOT X,Y-1:DRAWTO X,Y+1:PLOT X+1,Y+1
1191       L=PEEK(SPL-41)+PEEK(SPL-1)+PEEK(SPL+39)
1193       R=PEEK(SPL-39)+PEEK(SPL+1)+PEEK(SPL+42)
1195       C=PEEK(SPL+80)+PEEK(SPL+81)
1196     ENDIF :ENDIF :ENDIF 
1197 RETURN 
1200 IF ROT=0
1201   PLOT X-1,Y:DRAWTO X+1,Y:PLOT X+1,Y+1
1202   L=PEEK(SPL-2)+PEEK(SPL+40)
1203   R=PEEK(SPL+2)+PEEK(SPL+42)
1205   C=PEEK(SPL+39)+PEEK(SPL+40)+PEEK(SPL+81)
1207 ELSE :IF ROT=1
1210     PLOT X,Y-1:DRAWTO X,Y+1:PLOT X+1,Y-1
1211     L=PEEK(SPL-41)+PEEK(SPL-1)+PEEK(SPL+39)
1213     R=PEEK(SPL-38)+PEEK(SPL+1)+PEEK(SPL+41)
1215     C=PEEK(SPL+1)+PEEK(SPL+80)
1217   ELSE :IF ROT=2
1220       PLOT X-1,Y-1:PLOT X-1,Y:DRAWTO X+1,Y
1221       L=PEEK(SPL-42)+PEEK(SPL-2)
1223       R=PEEK(SPL-40)+PEEK(SPL+2)
1225       C=PEEK(SPL+39)+PEEK(SPL+40)+PEEK(SPL+41)
1227     ELSE 
1230       PLOT X,Y-1:DRAWTO X,Y+1:PLOT X-1,Y+1
1231       L=PEEK(SPL-41)+PEEK(SPL-1)+PEEK(SPL+38)
1233       R=PEEK(SPL-39)+PEEK(SPL+1)+PEEK(SPL+41)
1235       C=PEEK(SPL+79)+PEEK(SPL+80)
1236     ENDIF :ENDIF :ENDIF 
1237 RETURN 
1240 PLOT X,Y:PLOT X+1,Y:PLOT X+1,Y+1:PLOT X,Y+1
1241 L=PEEK(SPL-1)+PEEK(SPL+39)
1243 C=PEEK(SPL+80)+PEEK(SPL+81)
1245 R=PEEK(SPL+2)+PEEK(SPL+42)
1247 RETURN 
2000 IF ROT=0
2005   T=PEEK(SPL-1)+PEEK(SPL)+PEEK(SPL+1)+PEEK(SPL+2)
2007 ELSE :IF ROT=1
2010     T=PEEK(SPL-40)+PEEK(SPL)+PEEK(SPL+80)+PEEK(SPL+120)
2017   ELSE :IF ROT=2
2020       T=PEEK(SPL-2)+PEEK(SPL-1)+PEEK(SPL)+PEEK(SPL+1)
2027     ELSE 
2030       T=PEEK(SPL-80)+PEEK(SPL-40)+PEEK(SPL)+PEEK(SPL+40)
2035     ENDIF :ENDIF :ENDIF 
2037 RETURN 
2040 IF ROT=0
2045   T=PEEK(SPL-1)+PEEK(SPL)+DPEEK(SPL+40)
2047 ELSE :IF ROT=1
2050     T=PEEK(SPL-40)+DPEEK(SPL-1)+PEEK(SPL+39)
2057   ELSE :IF ROT=2
2060       T=PEEK(SPL-1)+PEEK(SPL)+DPEEK(SPL+40)
2067     ELSE 
2070       T=PEEK(SPL-40)+DPEEK(SPL-1)+PEEK(SPL+39)
2075     ENDIF :ENDIF :ENDIF 
2077 RETURN 
2080 IF ROT=0
2085   T=PEEK(SPL+1)+DPEEK(SPL-1)+PEEK(SPL+40)
2087 ELSE :IF ROT=1
2090     T=PEEK(SPL-41)+DPEEK(SPL-1)+PEEK(SPL+40)
2097   ELSE :IF ROT=2
2100       T=PEEK(SPL+1)+DPEEK(SPL-1)+PEEK(SPL+40)
2107     ELSE 
2110       T=PEEK(SPL-41)+DPEEK(SPL-1)+PEEK(SPL+40)
2115     ENDIF :ENDIF :ENDIF 
2117 RETURN 
2120 IF ROT=0
2125   T=PEEK(SPL)+DPEEK(SPL+39)+PEEK(SPL+41)
2127 ELSE :IF ROT=1
2130     T=DPEEK(SPL)+PEEK(SPL-41)+PEEK(SPL+39)
2137   ELSE :IF ROT=2
2140       T=PEEK(SPL)+DPEEK(SPL-40)+PEEK(SPL-41)
2147     ELSE 
2150       T=DPEEK(SPL)+PEEK(SPL-39)+PEEK(SPL+41)
2155     ENDIF :ENDIF :ENDIF 
2157 RETURN 
2160 IF ROT=0
2165   T=DPEEK(SPL-1)+PEEK(SPL)+PEEK(SPL+39)
2167 ELSE :IF ROT=1
2170     T=DPEEK(SPL-41)+PEEK(SPL)+PEEK(SPL+40)
2177   ELSE :IF ROT=2
2180       T=PEEK(SPL-39)+DPEEK(SPL-1)+PEEK(SPL+1)
2187     ELSE 
2190       T=PEEK(SPL-40)+PEEK(SPL)+DPEEK(SPL+40)
2195     ENDIF :ENDIF :ENDIF 
2197 RETURN 
2200 IF ROT=0
2205   T=PEEK(SPL-1)+DPEEK(SPL)+PEEK(SPL+41)
2207 ELSE :IF ROT=1
2210     T=DPEEK(SPL-40)+PEEK(SPL)+PEEK(SPL+40)
2217   ELSE :IF ROT=2
2220       T=PEEK(SPL-41)+DPEEK(SPL-1)+PEEK(SPL+1)
2227     ELSE 
2230       T=PEEK(SPL-40)+PEEK(SPL)+DPEEK(SPL+39)
2235     ENDIF :ENDIF :ENDIF 
2237 RETURN 
2240 T=PEEK(SPL)+PEEK(SPL+1)+PEEK(SPL+40)+PEEK(SPL+41)
2247 RETURN 
3000 GRAPHICS 0
3005 LV=INT(LV):HT=INT(HT)
3010 POKE 82,0
3012 POKE 710,32:POKE 709,8:POKE 712,32
3015 ? "$).....             T E T R I S"
3020 ? "..     ATARI VERSION, DAVID MASON"
3040 ? " COLOR & SOUND ADDED BY PETER J. MEYER"
3050 IF SC>HSC THEN HSC=SC
3055 IF LS>HLS AND (TYPE=1 OR TYPE=3 OR TYPE=4 OR TYPE=7) THEN HLS=LS
3058 IF LV>HLV THEN HLV=INT(LV)
3060 ? :? :? 
3065 ? ,"LAST","HIGHEST"
3070 ? "SCORE:",SC,HSC
3075 ? "LINES:",LS,HLS
3080 POSITION 4,18:? "SELECT LEVEL:";LV;"   ":POSITION 23,18:? "OPTION TYPE:";
3090 IF TYPE=1 THEN ? "A"
3091 IF TYPE=2 THEN ? "B":POSITION 18,20:? "HIGHT:";HT;"  "
3092 IF TYPE=3 THEN ? "C":NLS=10:POSITION 10,22
3093 IF TYPE=4 THEN ? "D":NLS=5
3094 IF TYPE=5 THEN ? "E":POSITION 18,20:? "HIGHT:";HT;"  "
3095 IF TYPE=6 THEN ? "F":POSITION 18,21:? "HIGHT:";HT;"  "
3096 IF TYPE=7 THEN ? "G":POSITION 18,20:? "HIGHT:";HT;"  "
3097 RESTORE 6200+TYPE:READ A$:POSITION 4,22:? A$
3100 REPEAT 
3105   REPEAT 
3110     P=PEEK(53279)&7
3115   UNTIL P<7 OR (TYPE=2 OR TYPE=5 OR TYPE=6 OR TYPE=7) AND PEEK(732)=17
3120   IF P&2=0 THEN LV=LV MOD HLV+1
3122   IF P&4=0 THEN TYPE=TYPE MOD 7+1
3123   POSITION 4,18:? "SELECT LEVEL:";LV;"   ":POSITION 23,18:? "OPTION TYPE:";:IF TYPE=1 THEN ? "A":LS=0
3124   IF TYPE=2 THEN ? "B":LS=25
3125   IF TYPE=3 THEN ? "C":LS=0:NLS=10
3126   IF TYPE=4 THEN ? "D":LS=0:NLS=4
3127   IF TYPE=5 THEN ? "E":LS=50
3128   IF TYPE=6 THEN ? "F":LS=100
3129   IF TYPE=7 THEN ? "G":LS=0
3130   IF TYPE=2 OR TYPE=5 OR TYPE=6 OR TYPE=7
3132     IF PEEK(732)=17 THEN HT=HT+1:REPEAT :POKE 732,0:UNTIL PEEK(732)=0:IF HT>6 THEN HT=0
3134     POSITION 11,22:? "HELP HIGHT:";HT;"  "
3136   ELSE 
3138     POSITION 11,22:? "               "
3139   ENDIF 
3140   RESTORE 6200+TYPE:READ A$:POSITION 4,22:? A$
3192   PAUSE 5
3195 UNTIL P&1=0
3999 RETURN 
4000 IF HT>6.5 THEN HT=6.5
4001 IF HP>21-HT*2 THEN HP=21-INT(HT*2):Q=W-68+HP*40
4002 FOR N=22.9 TO 22-INT(HT*2) STEP -0.15
4004   COLOR RAND(7)+1:PLOT 12+RAND(10),INT(N)
4005 NEXT N
4007 SC=SC+(HT^2)*1000
4008 GOSUB 5800
4009 RETURN 
4849 ------------------------------
5000 GRAPHICS 0:DL=DPEEK(560)-256:W=DL+40:DPOKE 560,DL:DPOKE 88,W
5005 POKE DL,32:POKE DL+1,66:DPOKE DL+2,W+920:POKE DL+4,66:DPOKE DL+5,W+40
5010 FOR N=DL+7 TO DL+31:POKE N,2:NEXT N
5015 POKE DL+32,65:DPOKE DL+33,DL
5020 POKE DL+28,130:POKE 623,128
5025 RESTORE 5500:FOR N=1536 TO 1557:READ A:POKE N,A:NEXT N
5030 DPOKE 512,1536:POKE 54286,192
5035 DPOKE 660,W+960:POKE 703,4
5040 MOVE 57344,45056,1024:POKE 756,176
5045 FOR N=0 TO 15:A=N+16*N:FOR M=45568+N*8 TO 45575+N*8:POKE M,A:NEXT M:NEXT N
5055 GOSUB 5800
5060 POKE 87,12
5065 POKE W,0:MOVE W,W+1,1020
5068 POKE 704,0
5070 POKE 705,68
5075 POKE 706,102
5080 POKE 707,180
5085 POKE 708,230
5090 POKE 709,28
5095 POKE 710,58
5100 POKE 711,130
5110 POKE 752,1
5120 COLOR 8:PLOT 22,0:DRAWTO 22,23:DRAWTO 11,23:DRAWTO 11,0:PLOT 26,7:DRAWTO 32,7:DRAWTO 32,12
5130 DRAWTO 26,12:DRAWTO 26,7:COLOR 15:PAINT 0,0:PAINT 39,0:COLOR 32:PAINT 15,5:PAINT 27,8
5135 COLOR 17:PLOT 23,0:DRAWTO 23,23
5140 COLOR 16:PLOT 33,7:COLOR 17:PLOT 33,8:DRAWTO 33,12:COLOR 18:PLOT 33,13
5145 COLOR 19:PLOT 26,13:COLOR 20:PLOT 27,13:DRAWTO 32,13
5190 RETURN 
5500 DATA 72,169,32,141,10,212,141,24,208,141,26,208,141,27,208,169,12,141,23,208,104,64
5800 POKE 45688,0:MOVE 45688,45689,48:C=RAND(2):D=((LV+1) MOD 10):IF D=0 THEN POKE 712,228:RETURN 
5801 IF D<>6
5802   C=0:IF D>5 THEN C=RAND(3)+1
5803   FOR M=1 TO 8
5804     A=RAND(8)+1:N=M:IF N=C THEN N=RAND(8)+1
5806     ON D GOSUB 5850,5860,5870,5880,5890,5850,5850,5870,5890
5808     IF PEEK(45687+A)=0
5810       POKE 45687+A,B
5812     ELSE 
5814       GOTO 5804
5816     ENDIF 
5818   NEXT M
5820 ELSE 
5822   IF D=6
5824     A=1+RAND(8):C=1+RAND(3):POKE 712,22:IF A=C THEN C=1+RAND(8)
5825     FOR N=1 TO 8
5826       POKE 45687+N,A+A*16
5827       IF N=3 THEN POKE 45687+N,C+A*16
5828       IF N=7 THEN POKE 45687+N,A+C*16
5831     NEXT N
5832   ENDIF 
5843 ENDIF 
5845 MOVE 45688,45696,40:DPOKE 45728,0:DPOKE 45730,0
5847 FOR N=45700 TO 45715:POKE N,PEEK(N)&15:NEXT N:FOR N=45720 TO 45723:POKE N,PEEK(N)&240:NEXT N
5848 RETURN 
5850 POKE 712,158:B=N+C*16:IF A&1=1 THEN B=N*16+C
5855 RETURN 
5860 POKE 712,220:B=N+N*16
5865 RETURN 
5870 POKE 712,86:B=N+16*C:IF A>3 THEN B=N*16+C
5875 RETURN 
5880 POKE 712,40:B=N+((4+N MOD 8)+1)*16
5885 RETURN 
5890 POKE 712,158:B=N+16*C:IF A&2=2 THEN B=N*16+C
5895 RETURN 
6001 DATA 60
6002 DATA 52
6003 DATA 45
6004 DATA 39
6005 DATA 34
6006 DATA 30
6007 DATA 26
6008 DATA 23
6009 DATA 20
6010 DATA 18
6011 DATA 16
6012 DATA 14
6013 DATA 12
6014 DATA 10
6015 DATA 9
6016 DATA 8
6017 DATA 7
6018 DATA 6
6019 DATA 5
6020 DATA 5
6021 DATA 4
6022 DATA 4
6023 DATA 3
6024 DATA 3
6025 DATA 3
6026 DATA 3
6027 DATA 3
6200 DATA 2
6201 DATA         PLAY FOR HIGH SCORE     
6202 DATA          WIPE OUT 25 LINES      
6203 DATA     NEW SCREEN FOR EVERY LEVEL  
6204 DATA WIPEOUT MORE LINES FOR NEW LEVEL
6205 DATA          WIPE OUT 50 LINES      
6206 DATA          WIPE OUT 100 LINES     
6207 DATA  PLAY FOR HIGH SCORE WITH HIGHT 
