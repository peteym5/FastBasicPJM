1 GOSUB 5000:HISC=0:SC=0:GOTO 8050
5 GRAPHICS 27:A=20:B=20:RX=5:RY=0:CS=0.2:GOSUB 2000:GOSUB 1000
10 X=(-(STICK(0)=11)+(STICK(0)=7))
20 Y=(-(STICK(0)=14)+(STICK(0)=13))
30 COLOR 0:PLOT A,B-4:DRAWTO A,B+5:IF RN=1 THEN GOSUB 220
35 A=A+X*5:B=B+Y*12
40 IF A<8 AND LE$="1" THEN GOSUB 1000
41 IF B<15 AND TE$="1" THEN GOSUB 1000
42 IF B>166 AND BE$="1" THEN GOSUB 1000
43 IF A>72 AND RE$="1" THEN GOSUB 1000
45 IF A<8 OR A>72 THEN A=A-X*5
46 IF B<15 OR B>166 THEN B=B-Y*12
50 COLOR 1:PLOT A,B-4:PLOT A,B-3:COLOR 11:DRAWTO A,B:COLOR 8:DRAWTO A,B+5
60 IF C1=1 THEN GOSUB 250
61 IF C1=2 THEN GOSUB 300
62 IF C1=3 THEN GOSUB 350
63 IF C1=4 THEN GOSUB 400
64 IF C1=5 THEN GOSUB 450
70 IF STRIG(0)=0 THEN GOSUB 500
110 IF X<>0 OR Y<>0 THEN SOUND 1,240,4,8:GOSUB 200:SOUND 1,0,0,0
120 IF TR=2 AND A>32 AND A<48 AND B>80 AND B<100 THEN GOSUB 750
190 GOTO 10
200 COLOR 11:PLOT A,B-2:DRAWTO A-2,B+2:PLOT A,B-2:DRAWTO A+2,B+2:RN=1:COLOR 0:PLOT A,B+5:DRAWTO A,B+1:COLOR 8
210 DRAWTO A+1,B+5:PLOT A,B+1:DRAWTO A-1,B+5:RETURN 
220 COLOR 0:PLOT A,B-2:DRAWTO A-2,B+2:PLOT A,B-2:DRAWTO A+2,B+2:RN=0:PLOT A,B+5:DRAWTO A,B+1
230 DRAWTO A+1,B+5:PLOT A,B+1:DRAWTO A-1,B+5:RETURN 
250 COLOR 0:PLOT C,D:DRAWTO C+2,D-1:PLOT C,D:DRAWTO C-2,D-1:PLOT C,D+1:DRAWTO C+4,D+2+CM:PLOT C,D+1:DRAWTO C-4,D+2+CM
260 CM=CM+MX:IF CM<0 OR CM>5 THEN MX=-MX
265 IF C>A-4 AND C<A+4 AND D>B-8 AND D<B+8 THEN POP :POP :GOTO 900
270 IF C<A THEN C=C+2*CS
275 IF C>A THEN C=C-2*CS
280 IF D>B THEN D=D-4*CS
285 IF D<B THEN D=D+4*CS
290 COLOR 5:PLOT C,D:DRAWTO C+2,D-1:PLOT C,D:DRAWTO C-2,D-1:COLOR 14:PLOT C,D+1:DRAWTO C+4,D+2+CM:PLOT C,D+1
291 DRAWTO C-4,D+2+CM
295 RETURN 
300 COLOR 0:PLOT C-1,D-3:DRAWTO C+1,D-3:PLOT C-1,D-2:DRAWTO C+1,D-2:COLOR 0:PLOT C,D:DRAWTO C,D+4
301 COLOR 0:PLOT C+1,D+1:DRAWTO C+4,D+4:PLOT C-1,D+1:DRAWTO C-4,D+4:DRAWTO C+4,D+4
310 IF C<A THEN C=C+1*CS
315 IF C>A THEN C=C-1*CS
320 IF D>B THEN D=D-3*CS
325 IF D<B THEN D=D+3*CS
345 COLOR 4:PLOT C-1,D-3:DRAWTO C+1,D-3:PLOT C-1,D-2:DRAWTO C+1,D-2:COLOR 1:PLOT C,D:DRAWTO C,D+4
346 COLOR 13:PLOT C+1,D+1:DRAWTO C+4,D+4:PLOT C-1,D+1:DRAWTO C-4,D+4:COLOR 7:DRAWTO C+4,D+4
347 IF C-8<A AND C+8>A AND D-24<B AND D+24>B THEN GOSUB 700
349 RETURN 
350 COLOR 3:PLOT C,D:DRAWTO C,D+6:COLOR 14:PLOT C+2,D+2:DRAWTO C+6,D+6:PLOT C-2,D+2:DRAWTO C-6,D+6:COLOR 4
355 PLOT C-2,D-5:DRAWTO C+2,D-5:COLOR 6:PLOT C-3,D-4:DRAWTO C+3,D-4:COLOR 8:PLOT C-3,D-3:DRAWTO C+3,D-3:PLOT C-3,D-2
360 DRAWTO C+3,D-2:COLOR 6:PLOT C-3,D-1:DRAWTO C+3,D-1:COLOR 4:PLOT C-2,D:PLOT C-1,D:PLOT C+1,D:PLOT C+2,D
365 COLOR 15:PLOT C+6,D+6:DRAWTO C-6,D+6
370 IF X=0 AND Y=0 THEN MX=MX+1:IF MX>10-CS*4.5 THEN 700
375 IF X<>0 OR Y<>0 THEN MX=0
390 RETURN 
400 COLOR 0:PLOT C,D:DRAWTO C+5,D+10:DRAWTO C,D+4:DRAWTO C-5,D+10:DRAWTO C,D:PLOT C,D+1:DRAWTO C+3,D+12
402 PLOT C,D+1:DRAWTO C-3,D+12
410 IF C<A THEN C=C+3*CS
415 IF C>A THEN C=C-3*CS
420 IF D+8>B THEN D=D-8*CS
425 IF D+8<B THEN D=D+8*CS
435 IF C>A-4 AND C<A+4 AND D>B-10 AND D<B+10 THEN POP :POP :GOTO 900
440 COLOR 12:PLOT C,D:DRAWTO C+5,D+10:DRAWTO C,D+4:DRAWTO C-5,D+10:DRAWTO C,D:COLOR 9:PLOT C,D+1:DRAWTO C+3,D+12
442 PLOT C,D+1:DRAWTO C-3,D+12:RETURN 

450 COLOR 0:PLOT C-1,D-3:DRAWTO C+1,D-3:PLOT C-2,D-2:DRAWTO C+2,D-2
451 PLOT C-2,D-1:DRAWTO C+2,D-1:PLOT C-2,D:DRAWTO C+2,D:PLOT C-2,D+1:DRAWTO C+2,D+1
452 PLOT C-1,D+2:DRAWTO C+1,D+2
460 C=C+CX*(2+CS*4):D=D+DX:IF D<20 OR D>160 THEN D=D-DX:DX=-DX:FOR N=15 TO 0 STEP -1.5:SOUND 0,8,10,N:NEXT N
465 IF C<15 OR C>65 THEN C=C-CX:CX=-CX:FOR N=14 TO 0 STEP -2:SOUND 0,16,14,N:NEXT N:SOUND 0,0,0,0
490 COLOR 7:PLOT C-1,D-3:DRAWTO C+1,D-3:COLOR 9:PLOT C-2,D-2:DRAWTO C+2,D-2
491 COLOR 11:PLOT C-2,D-1:DRAWTO C+2,D-1:COLOR 11:PLOT C-2,D:DRAWTO C+2,D:COLOR 9:PLOT C-2,D+1:DRAWTO C+2,D+1
492 COLOR 7:PLOT C-1,D+2:DRAWTO C+1,D+2
495 IF C>A-5 AND C<A+5 AND D>B-12 AND D<B+12 THEN POP :POP :GOTO 900
499 RETURN 


500 FOR N=15 TO 0 STEP -3:SOUND 1,255-N*16,10,N:NEXT N:SOUND 1,0,0,0:IF X=0 AND Y=0 THEN RETURN 
510 CF=0:FOR M=0 TO 3:COLOR 11:IF X<>0 THEN PLOT A,B-1:DRAWTO A+X*2,B-1
520 IF Y<>0 THEN PLOT A+1,B-1:DRAWTO A+1,B-1+Y*3
530 COLOR 2+M*4:IF A+5>C AND A-5<C AND Y<>0 AND C1>0 THEN DRAWTO A+1,INT(D):CF=1:GOTO 570
540 IF B+9>D AND B-9<D AND X<>0 AND C1>0 THEN DRAWTO C,B-1:CF=1:GOTO 570
550 IF Y<>0 THEN DRAWTO A+1,90+Y*86
560 IF X<>0 THEN DRAWTO 40+X*36,B-1
565 NEXT M:SETCOLOR 4,0,0:SETCOLOR 4,0,8
570 COLOR 0:IF Y<>0 THEN PLOT A+1,B-1:DRAWTO A+1,90+Y*86
580 IF X<>0 THEN PLOT A+X,B-1:DRAWTO 40+X*36,B-1
585 IF CF=1 THEN CF=0:GOTO 600
590 X=0:Y=0:RETURN 
600 COLOR 4:PLOT C,D:FOR N=0 TO 63:COLOR INT(RND(1)*16):PLOT C,D:DRAWTO C+INT(RND(1)*16)-8,D+INT(RND(1)*14)-7
602 SOUND 3,255,4,15.999-N/4:NEXT N
610 COLOR 0:FOR N=-14 TO 14:PLOT C-8,D+N:DRAWTO C+8,D+N:NEXT N:C1=0
620 GOTO 570
700 FOR N=15 TO 0 STEP -1.5:SOUND 1,N*10,2,N:NEXT N:COLOR 8
710 PLOT C,D:DRAWTO A,B:FOR N=0 TO 500:NEXT N:GOTO 8000
750 GRAPHICS 0:SETCOLOR 2,12,4:? "   YOU HAVE FOUND THE SPECTRIX!     "
755 FOR N=0 TO 255 STEP 4:SOUND 0,N,6,10:SOUND 1,N,10,10:SOUND 2,N,14,10:NEXT N:? :? :? :SOUND 2,0,0,0:SOUND 1,0,0,0
760 PRINT "   NOW RETURN HOME TO RECIEVE CREDIT      AND TO GO ON TO THE NEXT ROUND."
765 ? :? :? "   MOVE JOYSTICK TO CONTINUE PLAY."
770 IF STICK(0)=15 THEN 770
780 TR=3:GOSUB 1000:RETURN 
800 GRAPHICS 0:SETCOLOR 2,14,6:PRINT "    YOU HAVE RETURNED HOME SAFELY               WITH THE SPECTRIX!        ":? 
810 SC=SC+1:? :? :? :? " YOU HAVE FOUND ";SC;" SPECTRIXES."
820 ? :? :? :? :? :? "  PRESS FIRE BUTTON TO GO ON TO THE           NEXT ROUND OF PLAY."
830 IF STRIG(0)=1 THEN 830
840 RETURN 
900 FOR N=12 TO 0 STEP -0.5:COLOR 0:SETCOLOR 4,0,N:PLOT A-4,B-12+N*2:DRAWTO A+4,B-6+4:SOUND 3,N*5,10,14-N/4:NEXT N
910 GOTO 8000
1000 IF A<8 THEN A=70:RX=RX-1
1010 IF A>72 THEN A=8:RX=RX+1
1020 IF B<15 THEN B=155:RY=RY-1
1030 IF B>165 THEN B=15:RY=RY+1
1050 IF RY=0 THEN A$=R0$
1060 IF RY=1 THEN A$=R1$
1070 IF RY=2 THEN A$=R2$
1080 IF RY=3 THEN A$=R3$
1090 IF RY=4 THEN A$=R4$
1100 IF RY=5 THEN A$=R5$
1110 IF RY=6 THEN A$=R6$
1120 IF RY=7 THEN A$=R7$
1130 IF RY=8 THEN A$=R8$
1140 IF RY=9 THEN A$=R9$
1150 RM=RX-1:TE$=A$(RM*5+1,RM*5+1):LE$=A$(RM*5+2,RM*5+2):RE$=A$(RM*5+3,RM*5+3):BE$=A$(RM*5+4,RM*5+4)
1160 GRAPHICS 27:SETCOLOR 4,0,0:COLOR 10:IF TE$="0" THEN PLOT 0,0:DRAWTO 79,0:PLOT 1,2:DRAWTO 79,2
1170 IF BE$="0" THEN PLOT 0,179:DRAWTO 79,179:PLOT 1,177:DRAWTO 79,177
1180 IF LE$="0" THEN PLOT 0,0:DRAWTO 0,179:PLOT 2,0:DRAWTO 2,179
1190 IF RE$="0" THEN PLOT 79,0:DRAWTO 79,179:PLOT 77,0:DRAWTO 77,179
1200 COLOR 3:PLOT 0,0:DRAWTO 3,3:PLOT 79,0:DRAWTO 77,3:PLOT 0,179:DRAWTO 3,177:PLOT 79,179:DRAWTO 77,177
1210 C1=0:FOR N=0 TO 32:IF RX=CX(N) AND RY=CY(N) THEN C1=C1(N):C=40:D=90
1220 NEXT N
1230 IF C1=1 THEN MX=2:CM=2
1240 IF C1=2 THEN MX=0
1250 IF C1=3 THEN MX=0
1260 IF C1=5 THEN CX=1:DX=INT(RND(1)*5)+1
1480 IF TR>2 THEN 1520
1490 IF TR=2 THEN TR=1
1500 IF RX=E AND RY=F THEN FOR N=0 TO 15:COLOR N:PLOT 48-N,82:DRAWTO 32+N,98:PLOT 32,82+N:DRAWTO 48,98-N:NEXT N
1510 IF RX=E AND RY=F THEN TR=2:C1=0
1520 IF TR=3 AND RX=5 AND RY=0 THEN GOSUB 800:GOSUB 2000:TR=1:GOTO 1000
1530 IF RX=5 AND RY=0 THEN COLOR 15:PLOT 3,1:DRAWTO 77,1
1890 FOR N=0 TO 255 STEP 16:SOUND 0,255-N,10,12+N/64:SETCOLOR 4,0,N/31:NEXT N
1990 RETURN 
2000 FOR N=0 TO 32:CX(N)=INT(RND(1)*9)+1:CY(N)=INT(RND(1)*9)+1:C1(N)=INT(RND(1)*5)+1:NEXT N
2010 E=INT(RND(1)*9)+1:F=INT(RND(1)*7)+3:TR=1
2020 CS=CS+0.2:IF CS>2 THEN C=2
2099 RETURN 
5000 DIM R0$(45):R0$="                    0001                    "
5010 DIM R1$(45):R1$="0010 0111 0100 0011 1110 0101 0010 0111 0101"
5020 DIM R2$(45):R2$="0001 1011 0101 1011 0100 1011 0100 1001 1001"
5030 DIM R3$(45):R3$="1001 1001 1010 1110 0100 1001 0010 1100 1001"
5040 DIM R4$(45):R4$="1001 1010 0111 0100 0001 1011 0110 0111 1101"
5050 DIM R5$(45):R5$="1010 0101 1001 0011 1100 1001 0001 1001 1000"
5060 DIM R6$(45):R6$="0001 1011 1110 1111 0110 1111 1101 1011 0101"
5070 DIM R7$(45):R7$="1011 1101 0001 1011 0101 1001 1000 1001 1001"
5080 DIM R8$(45):R8$="1001 1000 1001 1000 1000 1000 0010 1101 1000"
5090 DIM R9$(45):R9$="1010 0110 1110 0100 0010 0110 0110 1110 0100"
5095 DIM A$(45),TE$(1),LE$(1),RE$(1),BE$(1):DIM CX(32),CY(32),C1(32)
5100 RETURN 
8000 GRAPHICS 18:POSITION 15,4:PRINT #6,"GAME OVER":FOR M=0 TO 9:FOR N=15 TO 0 STEP -1:SETCOLOR 2,13,N:NEXT N
8010 FOR N=0 TO 15 STEP 1:SETCOLOR 2,13,N:NEXT N:NEXT M
8020 POSITION 10,8:SETCOLOR 3,0,0:PRINT #6,"thankyou for playing"
8030 FOR N=0 TO 15 STEP 0.25:SETCOLOR 3,10,N:NEXT N:IF SC>HISC THEN HISC=SC
8040 FOR N=0 TO 500:NEXT N
8050 GRAPHICS 17:POSITION 13,6:SETCOLOR 2,13,6:PRINT #6,"SEARCH OF THE         SPECTRIX "
8060 POSITION 10,14:SETCOLOR 0,11,8:SETCOLOR 3,12,10:PRINT #6,"     CREATED BY        peter j. meyer"
8070 FOR N=0 TO 1000:IF STRIG(0)=0 THEN GOTO 5
8080 NEXT N:GRAPHICS 0:SETCOLOR 2,0,0:SETCOLOR 1,0,12:SETCOLOR 4,0,4:POKE 82,0
8090 ? :? :? :PRINT "     YOUR SCORE - ";SC
8100 ? :PRINT "     HIGH SCORE - ";HISC
8110 ? :? :? "PRESS BUTTON ON JOYSTICK TO START GAME."
8115 REM ? :? :? :? :? :? :? :? :? :? :? "     (C) Copyright 1986 __________"
8120 FOR N=0 TO 1500:IF STRIG(0)=0 THEN GOTO 5
8130 NEXT N:GRAPHICS 27
8140 FOR N=0 TO 15:COLOR N:PLOT 48-N,82:DRAWTO 32+N,98:PLOT 32,82+N:DRAWTO 48,98-N:NEXT N
8150 FOR N=0 TO 500:IF STRIG(0)=0 THEN GOTO 5
8160 NEXT N:GOTO 8050
32767 END 
